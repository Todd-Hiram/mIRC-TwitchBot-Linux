[script]
n0=alias -l MultiTimerRun {
n1=  if (%chat. [ $+ [ $1 ] ] >= 1) {
n2=    msg  $1 %multi. [ $+ [ $1 ] ]
n3=    set %chat. [ $+ [ $1 ] ] 0
n4=  }
n5=}
n6=
n7=on *:text:!streamer*:#:{
n8=  if ($msgtags(mod).key == 1 || $right(#,-1) == $nick) {
n9=    if (streamertwitchname == $2) {
n10=      set %streamer. [ $+ [ # ] $+ ] .streamertwitchname  $3
n11=      msg # Streamer twitch name has been set.
n12=    }
n13=    elseif (raid == $2) {
n14=      if ((@raidtarget@ !isin $3-) && (@link@ isin $3-)) { return }
n15=      else { 
n16=        set %streamer. [ $+ [ # ] $+ ] .raid $3- 
n17=        msg # Raid Channel Message has been set.
n18=      }
n19=    } 
n20=    elseif (multi == $2) {
n21=      if ((Moderator == $3) && ($4 == On || $4 == Off)) {
n22=        set %streamer. [ $+ [ # ] $+ ] .multi.mod $4
n23=        msg # The Moderator Only !multi has been set to $4 $+ .
n24=      }
n25=      elseif (@multitarget@ !isin $3-) { return }
n26=      else {
n27=        set %streamer. [ $+ [ # ] $+ ] .multi $3-
n28=        msg # Multi message has been set. 
n29=      }
n30=    }
n31=    elseif (sub == $2) {
n32=      if (@sub@ !isin $3-) { return }
n33=      else {
n34=        set %streamer. [ $+ [ # ] $+ ] .sub $3-
n35=        msg # Sub message has been set. 
n36=      }
n37=    }
n38=    elseif (resub == $2) {
n39=      if ((@sub@ !isin $3-) || (@month@ !isin $3-))  { return }
n40=      else {
n41=        set %streamer. [ $+ [ # ] $+ ] .resub $3-
n42=        msg # Resub message has been set. 
n43=      }
n44=    }
n45=    elseif (uptime == $2) {
n46=      if (@uptime@ !isin $3-) { return }
n47=      else { 
n48=        set %streamer. [ $+ [ # ] $+ ] .uptime.now $3-
n49=        msg # Uptime message has been set
n50=      }
n51=    }
n52=    elseif (offline == $2) {
n53=      set %streamer. [ $+ [ # ] $+ ] .uptime.offline $3- 
n54=      msg # Offline message has been set.
n55=    }
n56=    elseif (bits == $2) {
n57=      if (message == $3) {
n58=        set %streamer. [ $+ [ # ] $+ ] .bits.message $4- 
n59=        msg # Bits message has been set. 
n60=      }
n61=      elseif ((amount == $3) && ($4 isnum)) {
n62=        set %streamer. [ $+ [ # ] $+ ] .bits.amount $4
n63=        msg # Minimum cheer amount has been set. 
n64=      }
n65=    }
n66=    elseif (shoutout == $2) {
n67=      if ($3 == command) {
n68=        set %streamer. [ $+ [ # ] $+ ] .shoutout.command $4
n69=        msg # Shoutout command has been set.
n70=      }
n71=      elseif ($3 == message) { 
n72=        set %streamer. [ $+ [ # ] $+ ] .shoutout.message $4-
n73=        msg # Shoutout message has been set.
n74=      }
n75=    }
n76=    else { msg # $nick $+ , please check out [link] for info on how to use !streamer. }
n77=  }
n78=}
n79=
n80=
n81=on *:text:!multi*:#: {
n82=  if ($msgtags(mod).key == 1 || $right(#,-1) == $nick) {
n83=    if ($2 == set) {
n84=      set %multi. [ $+ [ # ] ] %streamer. [ $+ [ # ] $+ ] .multi
n85=      set %multi. [ $+ [ # ] ] $replace(%multi. [ $+ [ # ] ],@multitarget@,$3)
n86=      /timermulti [ $+ [ # ] ] 0 300 MultiTimerRun #
n87=      msg # Multistreaming with $3 has been turned on.
n88=      halt
n89=    }
n90=    elseif ( $2 == off) {
n91=      /timermulti [ $+ [ # ] ] off
n92=      msg # Multistreaming has been turned off. 
n93=      unset %multi. [ $+ [ # ] ]
n94=      halt
n95=    }
n96=  }
n97=  elseif (($msgtags(mod).key != 1 && $right(#,-1) != $nick) && (%streamer. [ $+ [ # ] $+ ] .multi.mod == On)) { halt }
n98=  msg # %multi. [ $+ [ # ] ]
n99=  set %chat. [ $+ [ $1 ] ] 0
n100=  /timermulti [ $+ [ # ] ] 0 30 MultiTimerRun #
n101=}
n102=
n103=
n104=
n105=on *:TEXT:!raid *:#: {
n106=  if (($msgtags(mod).key != 1) && ($right(#,-1) != $nick)) { return } 
n107=  if ($$0 < 2) { msg $chan Wrong! Incorrect! Use: !raid [channel name] }
n108=  elseif (twitch == $3) {
n109=    unset %streamer. [ $+ [ # ] $+ ] .raid.streamer. [ $+ [ $2 ] ]
n110=    var %raid. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .raid, @link@, https://www.twitch.tv/ , @raidtarget@, $$2)
n111=    .timerraid [ $+ [ # ] ] 4 0 msg # %raid. [ $+ [ # ] ]
n112=  }
n113=  elseif ((%streamer. [ $+ [ # ] $+ ] .raid.streamer. [ $+ [ $2 ] ] == beam) || ( $3 == beam)) {
n114=    set %streamer. [ $+ [ # ] $+ ] .raid.streamer. [ $+ [ $2 ] ] beam
n115=    var %raid. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .raid, @link@, https://www.beam.pro/ , @raidtarget@, $$2)
n116=    .timerraid [ $+ [ # ] ] 4 0 msg # %raid. [ $+ [ # ] ]
n117=  }
n118=  else { 
n119=    var %raid. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .raid, @link@, https://www.twitch.tv/ ,@raidtarget@, $2)
n120=    .timerraid [ $+ [ # ] ] 4 0 msg # %raid. [ $+ [ # ] ]
n121=  }
n122=}
n123=
n124=on *:text:*just subscribed*:#:{
n125=  if ($nick == twitchnotify) && ($4 != to) { 
n126=    var %sub. [ $+ [ # ] ] $replace(%streamer. [ $+ [ # ] $+ ] .sub,@sub@,$$1)
n127=    msg # %sub. [ $+ [ # ] ]
n128=  }
n129=}
n130=
n131=raw USERNOTICE:*:{
n132=  if (resub isin $msgtags) {
n133=    var %resub. [ $+ [ $1 ] ] $replace(%streamer. [ $+ [ $1 ] $+ ] .resub,@sub@,$msgtags(display-name).key,@month@,$msgtags(msg-param-months).key) 
n134=    msg $1 %resub. [ $+ [ $1 ] ]
n135=  }
n136=}
n137=
n138=on *:TEXT:!regular *:#:{
n139=  if ($msgtags(mod).key == 1 || $right(#,-1) == $nick)  {
n140=    if (add == $2) {
n141=      if (Regular. $+ # isin $level($3)) {
n142=        msg # /me $3 is already on the Regular List.
n143=      }
n144=      else {
n145=        auser -a Regular. [ $+ [ # ] ] $3
n146=      msg # /me $3 has been added to the Regular List. }
n147=    }
n148=    elseif (del == $2) {
n149=      if (Regular. $+ # isin $level($3)) {
n150=        ruser Regular. $+ # $3
n151=        msg # /me $3 has been removed from the Regular List.
n152=      }
n153=      else {
n154=        msg # /me $3 is not on the Regular List.
n155=      }
n156=    }
n157=  }
n158=}
n159=on *:text:!reloademotes:#:{
n160=  if (zapdos26 == $nick) {
n161=    /hfree -s allemotes
n162=    /hmake -s allemotes
n163=    /hload -s allemotes allemotes.txt
n164=    msg # Emotes have been reloaded!
n165=  }
n166=}
n167=
n168=
n169=on *:text:*:#:{
n170=  if ($msgtags(bits).key > 0) && ($msgtags(bits).key >= %streamer. [ $+ [ # ] $+ ] .bits.amount) {
n171=    set %bitsstreamermessage %streamer. [ $+ [ # ] $+ ] .bits.message
n172=    if ($msgtags(bits).key > 1) { var %bitsamountspecialthing = bits }
n173=    else { var %bitsamountspecialthing = bit }
n174=    var %bitsstreamermessage $replace(%bitsstreamermessage, @bits@, $msgtags(bits).key, @nick@, $msgtags(display-name).key,@bitsamountspecialthing@,%bitsamountspecialthing )
n175=    msg # %bitsstreamermessage
n176=  }
n177=  if ((%streamer. [ $+ [ # ] $+ ] .shoutout.command == $1) && ($msgtags(mod).key == 1 || $right(#,-1) == $nick)) {
n178=    if (twitch == $3) {
n179=      unset %streamer. [ $+ [ # ] $+ ] .shoutout.streamer. [ $+ [ $2 ] ]
n180=      var %shoutout $replace(%streamer. [ $+ [ # ] $+ ] .shoutout.message, @link@, https://www.twitch.tv/ , @nick@, $2)
n181=      msg # %shoutout
n182=    }
n183=    elseif ((%streamer. [ $+ [ # ] $+ ] .shoutout.streamer. [ $+ [ $2 ] ] == beam) || ($3 == beam)) {
n184=      set %streamer. [ $+ [ # ] $+ ] .shoutout.streamer. [ $+ [ $2 ] ] beam
n185=      var %shoutout $replace(%streamer. [ $+ [ # ] $+ ] .shoutout.message, @link@, https://www.beam.pro/ , @nick@, $2)
n186=      msg # %shoutout
n187=    }
n188=    else {
n189=      var %shoutout $replace(%streamer. [ $+ [ # ] $+ ] .shoutout.message, @link@, https://www.twitch.tv/ , @nick@, $2)
n190=      msg # %shoutout
n191=    }
n192=  }
n193=}
